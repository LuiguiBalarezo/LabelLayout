apply plugin: 'com.android.library'

ext {
    currentVersionCode = 2017102001
    currentVersionName = '1.1.0'

    groupId = 'com.luxiliu'
    artifactId = 'labellayout'

    packageRepo = 'maven'
    packageName = 'LabelLayout'
    packageDescription = 'A layout to display a label text on top of another view'
    packageWebsite = 'https://github.com/luxiliu/LabelLayout'
    packageIssueTracker = 'https://github.com/luxiliu/LabelLayout/issues'
    packageVCS = 'https://github.com/luxiliu/LabelLayout.git'
    packageLicenses = ["Apache-2.0"]
    packageLicenseName = 'The Apache License, Version 2.0'
    packageLicenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode currentVersionCode
        versionName currentVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.0.0'
    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.github.dcendents.android-maven'

install {
    repositories.mavenInstaller {
        pom.project {
            name packageName
            description packageDescription
            url packageWebsite
            inceptionYear '2017'

            packaging 'aar'
            groupId groupId
            artifactId artifactId
            version currentVersionName

            licenses {
                license {
                    name packageLicenseName
                    url packageLicenseUrl
                    distribution 'repo'
                }
            }
            scm {
                connection packageVCS
                developerConnection packageVCS
                url packageWebsite
            }
            developers {
                developer {
                    name 'Luxi Liu'
                    email 'luxi.liu@gmail.com'
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

version = currentVersionName
group = groupId

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
bintray {
    user = LABELLAYOUT_BINTRAY_USER
    key = LABELLAYOUT_BINTRAY_APIKEY

    configurations = ['archives']
    pkg {
        repo = packageRepo
        name = packageName
        desc = packageDescription
        websiteUrl = packageWebsite
        issueTrackerUrl = packageIssueTracker
        vcsUrl = packageVCS
        licenses = packageLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = LABELLAYOUT_BINTRAY_GPS_PASSWORD
            }
        }
    }
}
